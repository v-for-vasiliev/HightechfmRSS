apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "ru.vasiliev.hightechfmrss"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'armeabi-v7a'
            universalApk false
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes.all {
        buildConfigField "String", "API_BASE_URL", "\"https://hightech.fm\""
        buildConfigField "int", "ENCLOSURE_IMG_WIDTH", "1210"
        buildConfigField "int", "ENCLOSURE_IMG_HEIGHT", "600"
        buildConfigField "int", "PRELOAD_PAGER_FRAGMENTS", "2"
        buildConfigField "int", "PRELOAD_ARTICLES", "5"
        buildConfigField "int", "PRELOAD_ENCLOSURES", "3"
    }

    buildTypes {
        release {
            applicationIdSuffix ".release"
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "NETWORK_TRACE_STATE", "\"release\""
        }

        debug {
            applicationIdSuffix ".debug"
            debuggable true
            buildConfigField "String", "NETWORK_TRACE_STATE", "\"debug\""
        }

        /**
         * The `initWith` property allows you to copy configurations from other build types,
         * then configure just the settings you want to change. This one copies the debug build
         * type, and then changes the manifest placeholder and application ID.
         */
        staging {
            initWith debug
            applicationIdSuffix ".debugStaging"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    AndroidXAppDependecies.each {
        add(it.configuration, it.dependency)
    }
}
